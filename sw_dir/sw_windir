#!/bin/csh -f

# This script is used to convert link between linux <-> windows
# It uses the same database with outlook convert_link
# It will not trigger run_mapping (using df command) if not neccessary
#
# Usage:
#   ./sw_windir             : get windows link of current dir
#   ./sw_windir <linux_dir> : get windows link of <linux_dir>
#   ./sw_windir "<win_dir>" : get linux link of <win_dir>
#
# Changelog:
#     v0      Mar06,2018        TuanLa          New Creation 
#

set times   = 0
set src_dir = `readlink -f $0`
set src_dir = "$src_dir:h"

set linux_link = 1

#echo "$1"

START:
@ times++

if ($#argv <= 1) then
  echo ""
  echo ""
  if ("$1" == "") then
    set dir = `pwd`
  else
    set dir = "$1"
    if (`echo "$dir" | egrep '^/'\|'^./'\|'^[a-zA-Z0-9]' | wc -l` != 1) then
      set linux_link = 0
      set dir = `echo "$dir" | sed 's#\\\\*#/#g'`
    endif
    if (`readlink -e "$dir" | wc -l` != 0) then
      set dir = "`readlink -e ''$dir''`"
    endif
    set dir = `echo "$dir" | sed 's#/$##g ; s#^/*#/#'`
  endif
  #echo "Dir: $dir"

  set db = "~/sw_dir/covert_link.csv"
  if ((! -e $db) || ($times != 1)) then
  #if (($times != 1)) then
    ${src_dir}/run_mapping
    echo ""
  endif
  set dir_org = "$dir"
  if ($linux_link == 1) then    # CONVERT LINUX > WINDOWS
    while (1)
      #echo $dir
      if (`cat $db | cut -d, -f1 | egrep '^'$dir'$' | wc -l` == 1) then
        set mapped_to = `cat $db | egrep '^'$dir',' | cut -d, -f2 | sed 's#\\#\\\\#g'`
        #echo $mapped_to
        #set dir_win   = `echo "$dir_org" | sed 's#'${dir}'#'${mapped_to}'# ; s#/#\\\\#g'`
        set dir_win   = `echo "$dir_org" | sed 's#'${dir}'#'${mapped_to}'# ; s#/#\\#g'`
        #sed -i 's/rvc-vnas-01/rvc-vnas-01.renesas.com/g' $dir_win
        echo "$dir_win"
        echo "$dir_win" | ~/local/bin/xclip -selection clipboard
        echo ""
        break
      else
        set dir = "$dir:h"
        if ("$dir" == "") then
          if ($times == 1) then
            goto START
          else
            echo "Could not convert! Please check input link again!"
            break
          endif
        endif
      endif
    end
  else                          # CONVERT WINDOWS > LINUX
    while (1)
      #echo $dir
      if (`cat $db | cut -d, -f2 | sed 's#\\\\*#/#g' | egrep '^'$dir'$' | wc -l` == 1) then
        set mapped_to = `cat $db | sed 's#\\\\*#/#g' | egrep ','$dir'$' | cut -d, -f1`
        #echo $mapped_to
        set dir_lnx   = `echo "$dir_org" | sed 's#'${dir}'#'${mapped_to}'#'`
        echo "$dir_lnx"
        echo "$dir_lnx" | ~/local/bin/xclip -selection clipboard
        echo ""
        break
      else
        set dir = "$dir:h"
        if ("$dir" == "") then
          if ($times == 1) then
            goto START
          else
            echo "Could not convert! Please check input link again!"
            break
          endif
        endif
      endif
    end
  endif

else
  echo "Please input 1 link only!"
endif
